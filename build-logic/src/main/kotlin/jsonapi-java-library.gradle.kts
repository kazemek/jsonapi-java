import net.ltgt.gradle.errorprone.errorprone
import net.ltgt.gradle.nullaway.nullaway

plugins {
    `java-library`
    groovy
    jacoco
    id("net.ltgt.errorprone")
    id("net.ltgt.nullaway")
}

group = providers.gradleProperty("group").get()
version = providers.gradleProperty("version").get()

val libs = extensions.getByType<VersionCatalogsExtension>().named("libs")

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

dependencies {
    compileOnly(libs.findLibrary("jspecify").get())
    errorprone(libs.findLibrary("errorprone-core").get())
    errorprone(libs.findLibrary("nullaway").get())
    testImplementation(libs.findLibrary("spock-core").get())
    testImplementation(libs.findLibrary("groovy-all").get())
    testImplementation(libs.findLibrary("bytebuddy").get())
}

nullaway {
    onlyNullMarked.set(true)
    jspecifyMode.set(true)
}

tasks.named<JavaCompile>("compileJava").configure {
    options.errorprone {
        disableAllChecks.set(true)
        error("NullAway")
        error("RequireExplicitNullMarking")
        nullaway {
            error()
        }
    }
}

tasks.withType<Test>().configureEach {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
    // Centralized fixture wiring: every module's tests resolve the shared JSON:API 1.1 document
    // corpus and the pinned draft-schema fixtures from these two root-relative directories.
    // Contents are fingerprinted as relocatable inputs; absolute -D paths are supplied only at
    // execution time.
    jvmArgumentProviders.add(
        objects.newInstance<FixtureDirectoryArgumentProvider>().apply {
            fixturesDir.set(rootProject.layout.projectDirectory.dir("fixtures/jsonapi-1.1"))
            schemaFixturesDir.set(
                rootProject.layout.projectDirectory.dir("fixtures/jsonapi-schema/1.1-pr1603"),
            )
        },
    )
}

tasks.jacocoTestReport {
    dependsOn(tasks.test)
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}
